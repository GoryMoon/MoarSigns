
// For those who want the bleeding edge
buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

/*
// for people who want stable - not yet functional for MC 1.8.8 - we require the forgegradle 2.1 snapshot
plugins {
    id "net.minecraftforge.gradle.forge" version "2.0.2"
}
*/

configurations {
    deployerJars
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'net.minecraftforge.gradle.forge'

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

version = "${config.mc_version}"+ "-" + "${config.mod_version}"
group= "se.gory_moon" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "MoarSigns"

minecraft {
    version = "${config.mc_version}"+ "-" + "${config.forge_version}"
    runDir = "run"

    useDepAts = true // Required for JEI
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20160518"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    replaceIn "ModInfo.java"
    replace "@MOD_VERSION@", config.mod_version
    if (project.hasProperty("ms_sha1_signature")) {
        replace "@FINGERPRINT@", project.ms_sha1_signature
    }
}

repositories {

    maven { // NEI and friends
        name 'Chickenbones'
        url 'http://chickenbones.net/maven/'
    }

    maven {
        name 'Prof. Mobius'
        url 'http://mobiusstrip.eu/maven'
    }

    ivy {
        name "MineTweaker3"
        artifactPattern "http://minetweaker3.powerofbytes.com/download/[module]-[revision].[ext]"
    }

    maven {
        // Required for JEI
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }

    maven {
        name 'tterrag maven'
        url "http://maven.tterrag.com/"
    }
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"

    compile "mezz.jei:jei_${config.mc_version}:${config.dep_jei_version}"
    //runtime "mezz.jei:jei_${config.mc_version}:${config.dep_jei_version}"

    compile "codechicken:CodeChickenLib:${config.mc_version}-${config.cclib_version}:deobf"
    compile "codechicken:CodeChickenCore:${config.mc_version}-${config.ccc_version}:deobf"
    compile "codechicken:NotEnoughItems:${config.mc_version}-${config.nei_version}:deobf"

    compile "mcp.mobius.waila:Waila:" + config.waila_version /*+ "_" + config.mc_version*/ + ":dev"
    deobfCompile "mcjty.theoneprobe:TheOneProbe:1.9.4-1.0.6-17"

    //compile name: 'MineTweaker3', version: config.minetweaker_version, ext: 'jar', group: ''

    // you may put jars on which you depend on in ./libs
    // or you may define them like so..
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"

    // real examples
    //compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    //compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", config.mod_version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': config.mod_version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'moarSigns_at.cfg'
    }
}

task devJar(type: Jar, dependsOn: 'classes') {
    from(sourceSets.main.output) {
        include '**'
    }

    extension = 'jar'
    classifier = 'dev'
}

artifacts {
    archives devJar
    archives sourceJar
}

tasks.build.dependsOn('signJar')

if (!project.hasProperty("keystore_location")) // keystore stuff
    ext.keystore_location = "."
if (!project.hasProperty("keystore_password")) // keystore stuff
    ext.keystore_password = ""
if (!project.hasProperty("ms_keystore_alias")) // keystore stuff
    ext.ms_keystore_alias = ""

task signJar(dependsOn: ["jar", "devJar"]) {
    inputs.dir jar.destinationDir
    inputs.file keystore_location
    inputs.property "ms_keystore_alias", ms_keystore_alias
    inputs.property "keystore_password", keystore_password
    outputs.dir devJar.destinationDir

    // only sign if the keystore exists
    onlyIf {
        return keystore_location != "." && keystore_password != ""
    }
    // the actual action.. sign the jar.
    doLast {
        jar.destinationDir.eachFile { file ->
            if (!file.getPath().endsWith(".jar"))
                return; // skip non-jars

            logger.lifecycle "signing $file"
            ant.signjar(
                    destDir: file.getParentFile(), // same place it came from
                    jar: file,
                    keystore: keystore_location,
                    alias: ms_keystore_alias,
                    storepass: keystore_password
            )
        }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        if (project.hasProperty("maven_url")) {
            println 'Publishing to files server'

            configuration = configurations.deployerJars
            repository(url: project.maven_url) {
                authentication(userName: project.maven_username, password: project.maven_password)
            }
            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
            }

            pom.project {
                name project.archivesBaseName
                packaging 'jar'
                description 'MoarSigns'
                url 'https://github.com/GoryMoon/MoarSigns/'

                scm {
                    url 'https://github.com/GoryMoon/MoarSigns/'
                    connection 'scm:git:git://github.com/GoryMoon/MoarSigns.git'
                    developerConnection 'scm:git:git@github.com/GoryMoon/MoarSigns.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/GoryMoon/MoarSigns/issues'
                }

                licenses {
                    license {
                        name 'GNU Lesser General Public License 3.0'
                        url 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'Gory_Moon'
                        name 'Gory_Moon'
                        roles {
                            role 'developer'
                        }
                    }
                }
            }
        }
    }
}

idea { module { inheritOutputDirs = true } }